#SET world = world

static feature = features.village
static markerBlock = blocks.white_stained_glass
static entityId = "mcscript_feature_detector"
static summonString = "summon minecraft:area_effect_cloud ~$ ~$ ~$ {Duration:2147483647,Tags:['$']}"
static entities = @e[tag=$entityId]

run for @a at @s {
    kill(entities)
}

# checks all adjacent blocks if they are in the features bounding box.
inline fun checkBlocks() {
    if (not isFeature(feature) or isBlock(markerBlock)) {
        kill()
    } else {
        setBlock(markerBlock)
        execute(stringFormat(summonString, 0, 0, 1, entityId))
        execute(stringFormat(summonString, 0, 0, -1, entityId))
        execute(stringFormat(summonString, 0, 1, 0, entityId))
        execute(stringFormat(summonString, 0, -1, 0, entityId))
        execute(stringFormat(summonString, 1, 0, 0, entityId))
        execute(stringFormat(summonString, -1, 0, 0, entityId))
    }
}

shouldRun = False
hasRun = False
fun doTick() {
    if (not hasEntity(entities)) {
        if (not hasRun) {
            hasRun = True
            run for @a at @s execute(stringFormat(summonString, 0, -1, 0, entityId))
        } else {
            run for @a print("Finished.")
            shouldRun = False
            hasRun = False
        }
    }
    if (shouldRun) {
        run for @a actionbar("Running...")
        run for @e[tag=$entityId,limit=250] at @s {
            checkBlocks()
        }
    }
}

fun onTick() {
    if (shouldRun) doTick()
}