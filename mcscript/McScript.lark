start: statement*

statement: declaration _STATEMENT_SEPARATOR
         | static_declaration _STATEMENT_SEPARATOR
         | index_setter _STATEMENT_SEPARATOR
         | term_ip _STATEMENT_SEPARATOR
         | expression _STATEMENT_SEPARATOR
         | function_definition
         | "return " expression _STATEMENT_SEPARATOR -> return_
         | control_if
         | control_while
         | control_do_while
         | control_for
         | control_enum
         | control_struct
         | context_manipulator
         | COMMENT -> comment

block: "{" statement* "}"

function_parameter: IDENTIFIER ":" IDENTIFIER
parameter_list: "(" [ /self/ ] (function_parameter "," )* function_parameter? ")"
function_definition: /\bfun\b/ IDENTIFIER parameter_list [ "->" IDENTIFIER ] block

function_call: accessor "(" (expression "," )* expression? ")"

variable_declaration: IDENTIFIER ":" IDENTIFIER

declaration: accessor "=" expression
           | "(" accessor ( "," accessor )+ ")" "=" expression -> multi_declaration
static_declaration: _KEYWORD_STATIC declaration
index_setter: accessor "[" expression "]" "=" expression

term_ip: accessor (PLUS | MINUS | TIMES | DIVIDE | MODULO) "=" expression


expression: boolean_and
?boolean_and: boolean_or (AND boolean_or)*
?boolean_or: boolean_not (OR boolean_not)*
?boolean_not: NOT? _expression

_expression: comparison
          | term

// terms like (6 + 8) * -9
term: sum
?sum: product ((PLUS | MINUS) product)*
?product: value ((TIMES | DIVIDE | MODULO) value)*
value: INTEGER
     | DECIMAL
     | STRING
     | SELECTOR
     | (/\b(True|False)\b/) -> boolean_constant
     | unary_operation
     | tuple
     | "(" expression ")"
     | accessor
     | array_accessor
     | function_call

accessor: IDENTIFIER ("." IDENTIFIER)*
array_accessor: accessor "[" expression "]"

unary_operation: MINUS value

tuple: "(" ( expression "," )+ expression?  ")"

comparison: term (VERIFY_EQUAL | VERIFY_NOT_EQUAL | VERIFY_GREATER | VERIFY_GREATER_OR_EQUAL | VERIFY_LESS | VERIFY_LESS_OR_EQUAL) term

// control flow
control_if: _KEYWORD_IF expression block [_KEYWORD_ELSE block]
// hack to enable recursive context changes. ToDo: fix this
control_while: _KEYWORD_WHILE expression [ context_modifier ] block
control_do_while: _KEYWORD_DO [ context_modifier ] block _KEYWORD_WHILE expression

control_for: /\bfor\b/ IDENTIFIER /\bin\b/ expression block

control_enum: /\benum\b/ IDENTIFIER enum_block
enum_block: "{" ( enum_property _STATEMENT_SEPARATOR )* enum_property _STATEMENT_SEPARATOR "}"
enum_property: IDENTIFIER | (IDENTIFIER "=" INTEGER)

control_struct: _KEYWORD_STRUCT IDENTIFIER struct_block
struct_block: "{" ( (variable_declaration ",") | function_definition )* variable_declaration? "}"

context_manipulator: _KEYWORD_RUN ( context_modifier)+ block
context_modifier: _CONTEXT_FOR expression -> context_for
                | _CONTEXT_AT expression -> context_at
                | _CONTEXT_ABSOLUTE expression "," expression "," expression -> context_absolute
                | _CONTEXT_RELATIVE expression "," expression "," expression -> context_relative
                | _CONTEXT_LOCAL expression "," expression "," expression -> context_local
                | _CONTEXT_ANCHORED expression -> context_anchor


INTEGER: /[0-9]+/
DECIMAL: /[0-9]*\.[0-9]+/
STRING: /".*?"/ | /'.*?'/
SELECTOR: /@[parse](\[\w+=!?.+\])?/
PLUS: "+"
MINUS: "-"
TIMES: "*"
DIVIDE: "/"
MODULO: "%"
AND: /\band\b/
OR: /\bor\b/
NOT: /\bnot\b/

INCR_ONE: "++"
DECR_ONE: "--"

VERIFY_EQUAL: "=="
VERIFY_NOT_EQUAL: "!="
VERIFY_GREATER: ">"
VERIFY_GREATER_OR_EQUAL: ">="
VERIFY_LESS: "<"
VERIFY_LESS_OR_EQUAL: "<="

IDENTIFIER.0: /[a-z_][a-z_0-9]*/i

_CONTEXT_AT: /\bat\b/
_CONTEXT_FOR: /\bfor\b/
_CONTEXT_ABSOLUTE: /\babsolute\b/
_CONTEXT_RELATIVE: /\brelative\b/
_CONTEXT_LOCAL: /\blocal\b/
_CONTEXT_ANCHORED: /\banchored\b/

_KEYWORD_RUN: /\brun\b/
_KEYWORD_STATIC: /\bstatic\b/
_KEYWORD_STRUCT: /\bstruct\b/
_KEYWORD_IF: /\bif/
_KEYWORD_ELSE: /\belse\b/
_KEYWORD_WHILE: /\bwhile/
_KEYWORD_DO: /\bdo\b/

_STATEMENT_SEPARATOR: /\n|;\n?/
COMMENT: "#" /[^\n]/*
%ignore " " | "\n" | "\t"
